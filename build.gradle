buildscript {
    apply from: 'gradle/buildscript.gradle', to: buildscript
    dependencies {
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.18"
    }
}
apply plugin: 'nebula-aggregate-javadocs'

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'net.ltgt.apt'
    apply plugin: 'maven-publish'
    apply plugin: 'project-report'
    apply plugin: 'com.bmuschko.clover'
    apply plugin: "com.diffplug.gradle.spotless"
    apply plugin: 'io.spring.dependency-management'

    clover {
        report {
            html = true
        }
        
        compiler {
            debug = true
            additionalArgs = '-parameters'
        }
    }

    spotless {
        java {
            googleJavaFormat('1.5')
        }
    }

    def repos = [
            'http://repo.spring.io/snapshot',
            "http://repo.spring.io/libs-milestone",
            'http://artifacts.sunshower.io/repository/sunshower-snapshots',
            'http://artifacts.sunshower.io/repository/sunshower-releases',
            "http://oss.sonatype.org/content/repositories/snapshots",
            "http://www.gridgainsystems.com/nexus/content/repositories/external"
    ]


    test {
        useJUnitPlatform()
    }


    task sourceJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    
    task testArtifacts(type: Jar, dependsOn: testClasses) {
        classifier = 'tests'
        from sourceSets.test.output
    }


    dependencies {
        clover 'org.openclover:clover:4.2.0'
    }

    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url mavenRepositoryUrl

            credentials {
                username mavenRepositoryUsername
                password mavenRepositoryPassword
            }
        }
        repos.each { repo ->
            maven { url "$repo" }
        }
    }

    dependencyManagement {
        imports {
            mavenBom("io.sunshower.sdk:bom-imported:${version}") {
                bomProperties([
                        'env.version': '1.0.8.Final'
                ])
            }
        }
    }

    plugins.withType(JavaPlugin) {

        dependencies {
            annotationProcessor "org.projectlombok:lombok:1.18.2"
            compileOnly "org.projectlombok:lombok:1.18.2"
            testCompileOnly "org.projectlombok:lombok:1.18.2"
            compile 'org.eclipse.persistence:org.eclipse.persistence.moxy'
            compile 'org.hibernate.javax.persistence:hibernate-jpa-2.1-api'
            testCompile 'javax.mail:mail'
            testCompile('org.mockito:mockito-core')
            testCompile('org.junit.platform:junit-platform-launcher')
            testCompile('org.junit.platform:junit-platform-commons')
            testCompile('org.junit.jupiter:junit-jupiter-engine')
            testCompile('org.junit.vintage:junit-vintage-engine')
        }
    }


    publishing {
        repositories {
            maven {
                url mavenRepositoryUrl

                credentials {
                    username mavenRepositoryUsername
                    password mavenRepositoryPassword
                }
            }
        }

        publications {
            maven(MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
                artifact testArtifacts {
                    classifier "tests"
                }
            }
        }
    }

}


publishing {
    repositories {
        maven {
            url mavenRepositoryUrl

            credentials {
                username mavenRepositoryUsername
                password mavenRepositoryPassword 
            }
        }
    }

    publications {
        maven(MavenPublication) {
            artifact archiveReports {
                classifier 'reports'
            }
        }
    }
}

task archiveReports(type: Zip) {
    from "$buildDir/docs"
    include "/**/*"

    from "$buildDir/reports/clover"

    archiveName 'reports.zip'
}

assemble.dependsOn cloverAggregateReports
assemble.dependsOn 'aggregateJavadocs'

assemble.dependsOn archiveReports
archiveReports.mustRunAfter cloverAggregateReports
archiveReports.mustRunAfter 'aggregateJavadocs'


defaultTasks 'clean', 'build', 'cloverAggregateReports', 'aggregateJavadocs'
